#pragma kernel UpdatePaintMap
#pragma kernel Readback;

Texture2D PaintInput;
Texture2D SpectrogramMap;
SamplerState pointClampSampler;

RWTexture2D<float> PaintMap;
RWStructuredBuffer<float> ReadbackBuffer;

uint2 resolution;

float frequencyDisplayMin;
float frequencyDisplayMax;
float maxFrequencyInSpectrum;
int paintMode;

float2 RemapUV(float2 uv)
{
    float timeT = uv.x;
    float freqT = uv.y;
    // Remap frequency to display range
    float frequency = lerp(frequencyDisplayMin, frequencyDisplayMax, freqT);
    freqT = frequency / maxFrequencyInSpectrum;

    return float2(timeT, freqT); //
}

float Remap01(float v, float minVal, float maxVal)
{
    return saturate((v - minVal) / (maxVal - minVal));
}

float AmplitudeToDecibels(float amplitude)
{
    return 20 * log10(amplitude);
}

float DecibelsToAmplitude(float db)
{
    return pow(10, db / 20);
}

[numthreads(8,8,1)]
void UpdatePaintMap(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= resolution.x || id.y >= resolution.y) return;

    float2 fullMapUV = id.xy / (resolution - 1.0);
   
    float paint = 0;
    float frequency = fullMapUV.y * maxFrequencyInSpectrum;

    if (frequency >= frequencyDisplayMin && frequency <= frequencyDisplayMax)
    {
        float2 displayUV = float2(fullMapUV.x, Remap01(frequency, frequencyDisplayMin, frequencyDisplayMax));
        paint = PaintInput.SampleLevel(pointClampSampler, displayUV, 0).r;
    }
    
    PaintMap[id.xy] = paint;
}

[numthreads(8,8,1)]
void Readback(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= resolution.x || id.y >= resolution.y) return;

    float amplitude = SpectrogramMap[id.xy].r;
    float amplitudePaint = PaintMap[id.xy].r;

    float amplitudeReadback = 0;
    if (paintMode == 0) amplitudeReadback = max(amplitude, amplitudePaint * 0.3);
    if (paintMode == 1) amplitudeReadback = amplitudePaint;

    uint flatIndex = id.x * resolution.y + id.y;
    ReadbackBuffer[flatIndex] = amplitudeReadback;
}
